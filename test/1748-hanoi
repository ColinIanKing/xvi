#! /usr/bin/tclsh

# See if the towers of hanoi solving vi macros work

# Boilerplate

source tkterm
source term_expect

# Utility functions

# Does the screen image start with the lines in the given list
# and is the cursor at the expected position?
proc screen_is {crow ccol should} {
    global term
    if { ! [cursor_at $crow $ccol] } { return FALSE }
    for {set i 0} {$i < [llength $should]} {incr i} {
	set line [lindex $should $i]
	# List of lines is indexed from 0 by i but screen is indexed from 1
	set row [expr $i + 1]
	set onscreen [string trimright [$term get $row.0 $row.end]]
	if { $onscreen != $line } { return FALSE }
    }
    return TRUE
}

# Is the cursor at the specified row and column?
proc cursor_at {row col} {
    global term cur_row cur_col
    # There must be a snappier way to say this, but I can't find it!
    if { $cur_row == $row && $cur_col == $col } {
	return TRUE
    } else {
	return FALSE
    }
}

proc statusline {} {
    global term
    return [string trimright [$term get 24.0 24.end]]
}

# Tests begin

# Start xvi
exp_send "../src/xvi\r"

# Give it one second to read the macros
exp_send ":so hanoi\r"
set timeout 1
set screen [list "" "~"]
term_expect timeout { exit 100 } \
    { screen_is 1 0 $screen }

# There should be no error messsages on the status line
if { [statusline] != ":so hanoi" } {
    puts "After :so hanoi, status line is [statusline]"
    exit 11
}

# Run the initialization code
exp_send "I"
set screen [list "" "01234567" "0" "0" "" "" "T0123456n" "T0123456$" "/" "~"]
term_expect timeout { exit 101 } \
    { screen_is 9 0 $screen }

# Run the main loop
exp_send "L"
set timeout 10
set screen [list "" "0" "01234567" "0" "" "" "T0123456n" "T0123456$" "/" "~"]
term_expect timeout { exit 102 } \
    { screen_is 2 0 $screen }

exit 0
