#!/bin/sh
# -*- tcl -*-
# The next line is executed by /bin/sh, but not tcl \
exec tclsh "$0" ${1+"$@"}

#
# Check that it says "Interrupted" when it should.
#

source term
start_vi

# Set the status line to anything other than "Interrupted"
proc clear_statusline { } {
    exp_send "\x07"	;# Ctrl-G prints the current filename etc
    term_expect timeout { fail 110 } \
	{ statusline_isnt "Interrupted" }
}
	

# Tests begin

# Check it started up OK
term_expect timeout { fail 100 } \
	{ screen_is 1 0 [list "" "~"] }


# Interrupt it in keystroke command mode
#
# xvi and nvi say "Interrupted"; vim has its own message.

set vim FALSE	; # Are we running the tests using vim?

exp_send "\x03"
term_expect timeout { fail 101 } \
    { expr { [screen_is 1 0 [list "" "~"]] && \
	[statusline_is "Interrupted"] } } { } \
    { expr { [screen_is 1 0 [list "" "~"]] && \
	[statusline_starts "Type  :quit<Enter>  to exit Vim"] } } {
	set vim TRUE
    }


# Interrupt it after one char of a 2-char command
#
# vim doen't display anything when this happens but POSIX says:
# "SIGINT: If in open or visual command mode, the terminal shall be alerted."

clear_statusline
exp_send "z\x03"
term_expect timeout { fail 102 } \
    { expr { [screen_is 1 0 [list "" "~"]] &&
	     ( $vim || [statusline_is "Interrupted"] ) } }


# Interrupt it in insert mode

clear_statusline
exp_send "ii"
term_expect timeout { fail 103 } \
    { screen_is 1 1 "i" }
exp_send "\x03"
term_expect timeout { fail 104 } \
    { expr { [screen_is 1 0 [list "i" "~"]] &&
	     ( $vim || [statusline_is "Interrupted"] ) } }


# Interrupt it in replace mode

clear_statusline
exp_send "0Daabc\x1Bh"
term_expect timeout { fail 105 } \
    { screen_is 1 1 "abc" }
exp_send "RX"
term_expect timeout { fail 106 } \
    { screen_is 1 2 "aXc" }
exp_send "\x03"
term_expect timeout { fail 107 } \
    { expr { [screen_is 1 1 "aXc"] &&
	     ( $vim || [statusline_is "Interrupted"] ) } }

# Interrupt it in command line mode

clear_statusline
exp_send "0D:"
term_expect timeout { fail 108 } \
    { expr { [statusline_is ":"] && [screen_is $rows 1 [list "" "~"]] } }
exp_send "\x03"
term_expect timeout { fail 109 } \
    { expr { [screen_is 1 0 [list "" "~"]] &&
	     ( $vim || [statusline_is "Interrupted"] ) } }


# Interrupt it while doing infinite macro recursion

exp_send ":map x y\r"
exp_send ":map y x\r"
exp_send "x"
exp_send "\x03"
term_expect timeout { fail 111 } \
    { expr { [screen_is 1 0 [list "" "~"]] &&
	     ( $vim || [statusline_is "Interrupted"] ) } }

exp_send ":map! x axy\r"
exp_send "ax"
# The x should send it into a loop, appending content madly.
# POSIX says "If there is a currently executing command, it shall be aborted
# and a message displayed".
# and nvi shows "Interrupted: mapped keys discarded".

# Give it time to fill the screen with a's
sleep 1

# Interrupt it
exp_send "\x03"
term_expect timeout { fail 113 } \
    { expr { $vim || [statusline_starts "Interrupted"] } }

# Now it should react to ":"
exp_send ":"
term_expect timeout { fail 114 } \
    { expr { [statusline_is ":"] && [cursor_at $rows 1] } }

exp_send "q!\r"

exit 0
